plugins {
	id 'org.springframework.boot' version '2.5.9'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'jacoco'
	id 'com.diffplug.spotless' version '5.12.5'
	id 'com.avast.gradle.docker-compose' version '0.13.3'
	id 'org.sonarqube' version '3.2.0'
	id 'com.palantir.docker' version '0.32.0'
}

group = 'com.simonini.adidas'
version = '0.0.1-SNAPSHOT'

compileJava {
	sourceCompatibility = JavaVersion.VERSION_11
	targetCompatibility = JavaVersion.VERSION_11
}

jar {
	enabled = false
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'io.springfox:springfox-swagger2:2.9.2'
	implementation 'io.springfox:springfox-swagger-ui:2.9.2'
	implementation 'org.springframework.boot:spring-boot-starter-amqp'
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	compileOnly 'org.projectlombok:lombok'

	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
	useJUnitPlatform()
}

//dockerCompose.isRequiredBy(test)

spotless {
	java {
		importOrder()
		removeUnusedImports()
		googleJavaFormat()
		indentWithSpaces()
		trimTrailingWhitespace()
	}
}

build.dependsOn(spotlessApply)

test {
	finalizedBy jacocoTestReport
	useJUnitPlatform()
}

jacocoTestReport {
	reports {
		xml.enabled true
		html.enabled true
	}
	dependsOn test
}

docker {
	name "${project.name}:${project.version}"
	files "$buildDir/libs/${project.name}-${version}.jar"
	buildArgs([JAR_FILE: "${project.name}-${version}.jar"])
}

sonarqube {
	properties {
		property "sonar.projectKey", "lcssimonini_${project.name}"
		property "sonar.organization", "lcssimonini"
		property "sonar.host.url", "https://sonarcloud.io"
		property "sonar.exclusions", [
				"**/com/simonini/adidas/subscriptionapi/configuration/**",
				"**/com/simonini/adidas/subscriptionapi/domain/**",
				"**/com/simonini/adidas/subscriptionapi/error/**",
				"**/com/simonini/adidas/subscriptionapi/validator/**",
				"**/com/simonini/adidas/subscriptionapi/repository/**",
				"**/com/simonini/adidas/subscriptionapi/util/**",
				"**/com/simonini/adidas/subscriptionapi/resource/dto/**",
				"**/com/simonini/adidas/subscriptionapi/integrations/email/config/**",
				"**/com/simonini/adidas/subscriptionapi/SubscriptionApiApplication.java"
		]
	}
}
