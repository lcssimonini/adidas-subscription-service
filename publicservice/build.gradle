plugins {
	id 'org.springframework.boot' version '2.5.9'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'jacoco'
	id 'com.diffplug.spotless' version '5.12.5'
	id 'org.sonarqube' version '3.2.0'
	id 'com.palantir.docker' version '0.32.0'
	id 'com.avast.gradle.docker-compose' version '0.13.3'
}

group = 'com.simonini.adidas'
version = '0.0.1-SNAPSHOT'

compileJava {
	sourceCompatibility = JavaVersion.VERSION_11
	targetCompatibility = JavaVersion.VERSION_11
}

jar {
	enabled = false
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
	maven { url 'https://repo.spring.io/snapshot' }
	maven { url 'https://repo.spring.io/milestone' }
}

ext {
	set('springCloudVersion', "2020.0.2")
	set('testcontainersVersion', "1.15.3")
}

dependencies {
	implementation group: 'org.springframework.data', name: 'spring-data-commons', version: '2.6.2'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
	implementation 'io.springfox:springfox-swagger2:2.9.2'
	implementation 'io.springfox:springfox-swagger-ui:2.9.2'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

tasks.named('test') {
	useJUnitPlatform()
}


spotless {
	java {
		importOrder()
		removeUnusedImports()
		googleJavaFormat()
		indentWithSpaces()
		trimTrailingWhitespace()
	}
}

build.dependsOn(spotlessApply)

test {
	finalizedBy jacocoTestReport
	useJUnitPlatform()
}

dockerCompose.isRequiredBy(test)

jacocoTestReport {
	reports {
		xml.enabled true
		html.enabled true
	}
	dependsOn test
}

docker {
	name "${project.name}:${project.version}"
	files "$buildDir/libs/${project.name}-${version}.jar"
	buildArgs([JAR_FILE: "${project.name}-${version}.jar"])
}

sonarqube {
	properties {
		property "sonar.projectKey", "lcssimonini_${project.name}"
		property "sonar.organization", "lcssimonini"
		property "sonar.host.url", "https://sonarcloud.io"
		property "sonar.exclusions", [
				"**/com/simonini/adidas/publicservice/configuration/**",
				"**/com/simonini/adidas/publicservice/domain/**",
				"**/com/simonini/adidas/publicservice/error/**",
				"**/com/simonini/adidas/publicservice/validator/**",
				"**/com/simonini/adidas/publicservice/util/**",
				"**/com/simonini/adidas/publicservice/integrations/subscriptions/resource/dto/**",
				"**/com/simonini/adidas/publicservice/integrations/subscriptions/client/**",
				"**/com/simonini/adidas/publicservice/PublicServiceApplication.java"
		]
	}
}
